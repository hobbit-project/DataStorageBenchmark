sparql select ?fr ?first ?last ?ct1 ?ct2 ((?ct1 + ?ct2) as ?sum) #Q3
where { 
     {select distinct ?fr ?first ?last ?ct1 ?ct2
      where {
         {select ?fr (count(*) as ?ct1)
          where {
            ?post snvoc:hasCreator ?fr .
            ?post snvoc:creationDate ?date .
            filter (?date >= "%startDate%"^^xsd:dateTime && ?date < "%startDate%"^^xsd:dateTime + xsd:duration("P%durationDays%D")) .
            ?post snvoc:isLocatedIn dbpedia:%countryXName%
          }
 	 group by ?fr
         }
         {select ?fr (count (*) as ?ct2)
          where {
            ?post2 snvoc:hasCreator ?fr .
            ?post2 snvoc:creationDate ?date2 .
            filter (?date2 >= "%startDate%"^^xsd:dateTime && ?date2 < "%startDate%"^^xsd:dateTime + xsd:duration("P%durationDays%D")) .
            ?post2 snvoc:isLocatedIn dbpedia:%countryYName%
          }
 	 group by ?fr
         }
        {sn:pers%personId% snvoc:knows ?fr.} union {sn:pers%personId% snvoc:knows ?fr2. ?fr2 snvoc:knows ?fr. filter (?fr != sn:pers%personId%)} .
        optional { ?fr snvoc:firstName ?first } .
	optional { ?fr snvoc:lastName ?last } .
        ?fr snvoc:isLocatedIn ?city .
	filter(!exists {?city snvoc:isPartOf dbpedia:%countryXName%}).
	filter(!exists {?city snvoc:isPartOf dbpedia:%countryYName%}).
     }
    }.
    filter (?ct1 > 0 && ?ct2 > 0) .
}
order by desc(?sum) ?fr
limit 20
